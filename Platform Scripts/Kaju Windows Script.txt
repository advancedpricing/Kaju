@ECHO OFF 

REM
REM These will be filled in by the calling app
REM

SET APP_NAME=@@APP_NAME@@
SET APP_PARENT=@@APP_PARENT@@
SET APP_VERSION=@@APP_VERSION@@
SET NEW_APP_NAME=@@NEW_APP_NAME@@
SET NEW_APP_PARENT=@@NEW_APP_PARENT@@
SET TEMP_FOLDER_PATH=@@TEMP_FOLDER@@
SET DECOMPRESSED_FOLDER_PATH=@@DECOMPRESSED_FOLDER@@
SET PID_FILE=@@PID_FILE_PATH@@

REM
REM -----------------
REM

SET TODAY_DATE=%DATE:~10,4%-%DATE:~4,2%-%DATE:~7,2% %TIME:~0,2%:%TIME:~3,2%:%TIME:~6,2%
SET APP_PATH=%APP_PARENT%\%APP_NAME%

SET BACKUP_PARENT=%APP_PATH%-%DATE:~10,4%%DATE:~4,2%%DATE:~7,2%%TIME:~0,2%%TIME:~3,2%%TIME:~6,2%

SET LOGGER=%APP_PARENT%\%NEW_APP_NAME% Update Log.txt
ECHO "STARTED ON %TODAY_DATE%" >> "%LOGGER%" 2>&1

FOR /L %%i IN (1,1,10) DO (
	IF NOT EXIST "%PID_FILE%" (
		GOTO :program_exited
	)

	REM Windows version of sleep 1. Starting in Windows Vista, the sleep command was removed.
	ping -n 2 127.0.0.1 >nul

	IF %%i == 10 (
		ECHO ERROR: Could not update app, it never quit >> "%LOGGER%" 2>&1
		EXIT /B 1
	)
)
:program_exited

mkdir "%BACKUP_PARENT%"

SET PROCEED=1

REM
REM Move the other items
REM
ECHO "Copying items to backup %BACKUP_PARENT%" >> "%LOGGER%" 2>&1

REM We will need to manually populate these move commands. Windows Batch doesn't really handle arrays,
REM only looping through space delimited elements of a string. Below is a template for moving one such file.

REM BEGIN PSEUDO-ARRAY
SET THIS_ITEM=@@OTHER_NAME@@
SET THIS_PATH=%APP_PARENT%\%THIS_ITEM%
ECHO "Looking for item %THIS_PATH%" >> "%LOGGER%" 2>&1
IF EXIST "%THIS_PATH%" (
	GOTO :copy_@@OTHER_NAME_WO_SPACES@@
)
ECHO "...not found as file, trying as directory" >> "%LOGGER%" 2>&1
IF EXIST "%THIS_PATH%\NUL" (
	GOTO :copy_@@OTHER_NAME_WO_SPACES@@
) ELSE (
	ECHO "...NOT FOUND!" >> "%LOGGER%" 2>&1
	GOTO :finished_with_@@OTHER_NAME_WO_SPACES@@
)

:copy_@@OTHER_NAME_WO_SPACES@@

ECHO "...found, copying" >> "%LOGGER%" 2>&1
COPY "%THIS_PATH%" "%BACKUP_PARENT%" >> "%LOGGER%" 2>&1
IF %ERRORLEVEL% NEQ 0 (
	ECHO "...FAILED! (Error %ERRORLEVEL%)" >> "%LOGGER%" 2>&1
	SET PROCEED=0
	GOTO :restore_from_backup
) ELSE (
	ECHO "...confirmed" >> "%LOGGER%" 2>&1
)

:finished_with_@@OTHER_NAME_WO_SPACES@@

REM END PSEUDO-ARRAY

REM
REM Move the executable to backup
REM
IF %PROCEED% == 1 (
	ECHO "Moving the executable %APP_NAME% to backup" >> "%LOGGER%" 2>&1
	MOVE "%APP_PATH%" "%BACKUP_PARENT%" >> "%LOGGER%" 2>&1
	IF %ERRORLEVEL% NEQ 0 (
		ECHO "...FAILED! (Error %ERRORLEVEL%)" >> "%LOGGER%" 2>&1
		SET PROCEED=0
		GOTO :restore_from_backup
	) ELSE (
		ECHO "...confirmed" >> "%LOGGER%" 2>&1
	)
)

REM
REM Make sure there wasn't an error during the move
REM
IF %PROCEED% == 1 (
	ECHO "All items moved to backup" >> "%LOGGER%" 2>&1
)

REM
REM Copy in the replacement files
REM

IF %PROCEED% == 1 (
	ECHO "Copying files from %NEW_APP_PARENT% to folder %APP_PARENT%" >> "%LOGGER%" 2>&1
	XCOPY /g /y /e /k "%NEW_APP_PARENT%" "%APP_PARENT%" >> "%LOGGER%" 2>&1
	IF %ERRORLEVEL% NEQ 0 (
		ECHO "...FAILED! (Error %ERRORLEVEL%)" >> "%LOGGER%" 2>&1
		SET PROCEED=0
		GOTO :restore_from_backup
	) ELSE (
		ECHO "...confirmed" >> "%LOGGER%" 2>&1
	)
)

REM
REM If we get here, it all worked
REM
IF %PROCEED% == 1 (
	GOTO :all_succeeded
)

:restore_from_backup
IF %PROCEED% == 0 (
	ECHO  "Attempting to restore old application" >> "%LOGGER%" 2>&1

	XCOPY /g /y /e /k "%BACKUP_PARENT%" "%APP_PARENT%" >> "%LOGGER%" 2>&1
	IF %ERRORLEVEL% EQU 0 (
		RMDIR /S /Q "%BACKUP_PARENT%" >> "%LOGGER%" 2>&1
	)
)
GOTO :launch_application

:all_succeeded
REM
REM Remove the backup and decompressed folders if everything has gone swimmingly so far
REM
IF %PROCEED% == 1 (
	ECHO "Removing backup" >> "%LOGGER%" 2>&1
	RMDIR /S /Q "%BACKUP_PARENT%" >> "%LOGGER%" 2>&1
	ECHO "Removing decompressed folder" >> "%LOGGER%" 2>&1
	RMDIR /S /Q "%DECOMPRESSED_FOLDER_PATH%" >> "%LOGGER%" 2>&1
)

REM
REM Launch the application
REM
:launch_application
IF %PROCEED% == 1 (
	ECHO "Launching new app" >> "%LOGGER%" 2>&1
	START "" "%APP_PARENT%\%NEW_APP_NAME%" --kaju-success %APP_VERSION%
	GOTO :remove_backup_folder
) ELSE (
	ECHO "Launching old app" >> "%LOGGER%" 2>&1
	START "" "%APP_PATH%" --kaju-fail
)

:remove_backup_folder
ECHO "Removing temp folder" >> "%LOGGER%" 2>&1
RMDIR /S /Q "%TEMP_FOLDER_PATH%" >> "%LOGGER%" 2>&1
